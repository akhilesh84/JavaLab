plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.springframework.boot' version '4.0.0-M2' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

group = 'com.demo'
version = '0.0.1'

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/milestone'
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
}

tasks.register('dockerBuild', Exec) {
    group = 'docker'
    description = 'Builds Docker image for webapi project'

    // Use full path to docker command
    def dockerPath = ['/usr/local/bin/docker', '/opt/homebrew/bin/docker'].find { file(it).exists() } ?: 'docker'

    commandLine dockerPath, 'build', '-t', 'javaapi:latest', '.'

    doFirst {
        println 'Building Docker image for webapi...'
        println "Using Docker at: ${dockerPath}"
    }

    doLast {
        println 'Docker image javaapi:latest built successfully'
    }
}
