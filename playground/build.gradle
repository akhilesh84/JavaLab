plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow'
}

application {
    mainClass = 'com.demo.Main'
}

dependencies {
    implementation project(':mylib')

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.reflections:reflections:${reflectionsVersion}"
    implementation "org.apache.kafka:kafka-clients:${kafkaClientsVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
}

shadowJar {
    archiveClassifier = ''
    manifest {
        attributes 'Main-Class': 'com.demo.Main'
    }
}

// Disable distribution tasks to avoid conflicts with shadowJar
distZip.enabled = false
distTar.enabled = false
startScripts.enabled = false

tasks.named('startShadowScripts') {
    enabled = false
}
tasks.named('shadowDistZip') {
    enabled = false
}
tasks.named('shadowDistTar') {
    enabled = false
}

// Task to run with javaagent
task runWithAgent(type: JavaExec) {
    group = 'application'
    description = 'Run the application with the agent attached'

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.demo.Main'

    def agentJar = project(':agent').tasks.shadowJar.outputs.files.singleFile
    jvmArgs "-javaagent:${agentJar.absolutePath}"

    dependsOn ':agent:shadowJar'
}

run {
    // Configure the standard run task to also use the agent
    def agentJar = project(':agent').tasks.shadowJar.outputs.files.singleFile
    jvmArgs "-javaagent:${agentJar.absolutePath}"

    dependsOn ':agent:shadowJar'
}

